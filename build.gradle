buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath "org.flywaydb:flyway-mysql:10.15.0"
	}
}
plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.3'
	id 'io.spring.dependency-management' version '1.1.4'
	id "org.flywaydb.flyway" version "10.15.0"
	id "org.openapi.generator" version "7.6.0"
	id 'com.diffplug.spotless' version '6.13.0'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}
configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}



dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.3'
	implementation "org.springframework.boot:spring-boot-starter-security"
	implementation 'org.springframework.session:spring-session-data-redis'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.17.2'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.mysql:mysql-connector-j'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter-test:3.0.3'
	testImplementation 'org.springframework:spring-webflux'
}

tasks.named('test') {
	useJUnitPlatform()
}

flyway {
	url = 'jdbc:mysql://localhost:3306/apidb'
	user = 'apiuser'
	password = 'apipass'
	locations = [
			'filesystem:src/main/resources/db/migration',
			'filesystem:src/main/resources/db/migration_dev' // 開発用サンプルデータ
	]
}


def openApiSpecPath = "$projectDir/src/main/resources/openapi.yaml"
def openApiGeneratedCodePath = layout.buildDirectory.dir("spring").get().toString()
openApiValidate {
	inputSpec = openApiSpecPath
}

openApiGenerate {
	generatorName.set("spring")
	inputSpec.set(openApiSpecPath)
	outputDir.set(openApiGeneratedCodePath)
	apiPackage.set("com.example.blog.api")
	modelPackage.set("com.example.blog.model")
	configOptions.put("interfaceOnly", "true")
	configOptions.put("useSpringBoot3", "true")
	configOptions.put("documentationProvider", "none")
}

sourceSets.main.java.srcDir "${openApiGeneratedCodePath}/src/main/java"
def openApiGeneratedCodeRelativePath = project.relativePath(openApiGeneratedCodePath)
spotless {
	java {
		target "${openApiGeneratedCodeRelativePath}/src/main/java/**/*.java".toString()
		removeUnusedImports()
	}
}